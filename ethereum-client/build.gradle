buildscript {
    ext {
        coroutines_version = "1.3.7"
        protobuf_version = "3.11.1"
        grpc_version = "1.29.0"
        grpc_kotlin_version = "0.1.4"
    }
    // This is a workaround to issue
    // https://github.com/web3j/web3j-gradle-plugin/issues/34
    dependencies {
        classpath ("org.web3j:codegen:4.6.3") {
            force = true
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'application'
    id 'idea'
    id 'com.google.protobuf' version '0.8.12'
    // The Web3J plugin is not compatible with Gradle 6 because of this issue:
    // https://github.com/web3j/web3j-gradle-plugin/issues/31
    id 'org.web3j' version '4.5.11'
}

repositories {
    jcenter()
    mavenCentral()
    // Local Maven repository
    maven {
        url 'file://Users/allirvin/.m2/repository'
    }
}

configurations.all { configuration ->
    // Workaround for dependency configuration issue:
    // https://github.com/google/protobuf-gradle-plugin/issues/391
    if (name.contains('Proto')) {
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    // Functional Kotlin library
    implementation "io.arrow-kt:arrow-core:0.10.4"

    // gRPC and protobuf dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    // Library for JSON serialisation and deserialisation
    implementation 'com.google.code.gson:gson:2.8.6'

    // Library for interacting with Ethereum
    implementation "org.web3j:core:4.6.3"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.3"
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

// This is a workaround to the issue where compileSolidity comes before compileKotlin
// https://github.com/web3j/web3j-gradle-plugin/issues/24
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn 'generateContractWrappers'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn 'generateTestContractWrappers'
}

sourceSets {
    main.kotlin.srcDirs += "${web3j.generatedFilesBaseDir}/main/java"
    test.kotlin.srcDirs += "${web3j.generatedFilesBaseDir}/test/java"
    main.proto.srcDirs += "../proto"
}

application {
    // Define the main class for the application.
    mainClassName = 'commitment.agent.ethereum.client.AppKt'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        // Specify protoc to generate using kotlin protobuf plugin
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        // Specify protoc to generate using our grpc kotlin plugin
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}

web3j {
    generatedPackageName = 'commitment.agent.contracts.generated'
    excludedContracts = ['Migration', 'StringUtils']
}

solidity {
    executable = "solc"
    version = "0.7.1"
}